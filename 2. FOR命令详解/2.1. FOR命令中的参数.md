## 2.1. FOR命令中的参数

### 1. /d

```
FOR /D %variable IN (set) DO command [command-parameters]
```

如果集中包含通配符，则指定与目录名匹配，而不与文件名匹配。

如果Set（也就是上面写的“相关文件或命令”）包含通配符（`*`和`?`），将对与Set 相匹配的每个目录（而不是指定目录中的文件组）执行指定的Command。

这个参数主要用于目录搜索，不会搜索文件，看这样的例子：

```
@echo off
for /d %%i in (c:\*) do echo %%i
pause
```

运行会把C盘根目录下的全部目录名字打印出来，而文件名字一个也不显示！

再来一个，比如我们要把当前路径下文件夹的名字只有1-3个字母的打出来：

```
@echo off
for /d %%i in (???) do echo %%i
pause
```

这样的话如果你当前目录下有目录名字只有1-3个字母的，就会显示出来，没有就不显示了。

这里解释下`*`号和`?`号的作用，`*`号表示任意N个字符，而`?`号只表示任意一个字符

知道作用了，给大家个思考题目：

```
@echo off
for /d %%i in (window?) do @echo %%i
pause
```

保存到C盘下执行，会显示什么呢？自己看吧！显示：

```
windows
```

`/D`参数只能显示当前目录下的目录名字，这个大家要注意！

### 2. /r

```
FOR /R [[drive:]path] %variable IN (set) DO command [command-parameters]
```

检查以[drive:]path为根的目录树，指向每个目录中的FOR语句。如果在`/R`后没有指定目录，则使用当前目录。如果集仅为一个单点`.`字符，则枚举该目录树。

递归

上面我们知道，`/D`只能显示当前路径下的目录名字，那么现在这个`/R`也是和目录有关，他能干嘛呢？放心他比`/D`强大多了！

他可以把当前或者你指定路径下的文件名字全部读取，注意是文件名字，有什么用看例子！请注意2点：

1. set中的文件名如果含有通配符(`?`或`*`)，则列举`/R`参数指定的目录及其下面的所有子目录中与set相符合的所有文件，无相符文件的目录则不列举。
2. 相反，如果set中为具体文件名，不含通配符，则枚举该目录树（即列举该目录及其下面的所有子目录），而不管set中的指定文件是否存在。这与前面所说的单点`.`枚举目录树是一个道理，单点代表当前目录，也可视为一个文件。

例：

```
@echo off
for /r c:\ %%i in (*.exe) do echo %%i
pause
```

把这个BAT保存到D盘随便哪里然后执行，我们会看到，它把C盘根目录，和每个目录的子目录下面全部的EXE文件都列出来了！

例：

```
@echo off
for /r %%i in (*.exe) do @echo %%i
pause
```

参数不一样了！这个命令前面没加`C:\`，也就是搜索路径，这样他就会以当前目录为搜索路径，比如你这个BAT你把他放在`d:\test`目录下执行，那么他就会把`d:\test`目录和下面的子目录的全部EXE文件列出来！

例：

```
@echo off
for /r c:\ %%i in (boot.ini) do echo %%i
pause
```

运行本例发现枚举了C盘所有目录，为了只列举存在boot.ini的目录，可改成下面这样：

```
@echo off
for /r c:\ %%i in (boot.ini) do if exist %%i echo %%i
pause
```

用这条命令搜索文件真不错。

### 3. /l

```
FOR /L %variable IN (start,step,end) DO command [command-parameters]
```

该集表示以增量形式从开始到结束的一个数字序列。因此，(1,1,5)将产生序列(1 2 3 4 5)，(5,-1,1)将产生序列(5 4 3 2 1)。

使用迭代变量设置起始值(Start#)，然后逐步执行一组范围的值，直到该值超过所设置的终止值(End#)。`/L`将通过对Start#与End#进行比较来执行迭代变量。如果Start#小于End#，就会执行该命令。如果迭代变量超过End#，则命令解释程序退出此循环。还可以使用负的Step#以递减数值的方式逐步执行此范围内的值。例如，(1,1,5)生成序列(1 2 3 4 5)，而(5,-1,1)则生成序列(5 4 3 2 1)。

例：

```
@echo off
for /l %%i in (1,1,5) do @echo %%i
pause
```

保存执行看效果：

```
1
2
3
4
5
请按任意键继续...
```

(1,1,5)这个参数也就是表示从1开始每次加1直到5终止!

例：

```
@echo off
for /l %%i in (1,1,5) do start cmd
pause
```

执行后是不是吓了一跳，怎么多了5个CMD窗口！如果把那个(1,1,5)改成(1,1,65535)会有什么结果，我先告诉大家，会打开65535个CMD窗口...这么多你不死机算你强！当然我们也可以把那个`start cmd`改成`md %i`这样就会建立指定个目录了，名字为1-65535。

### 4. /f

迭代及文件解析

使用文件解析来处理命令输出、字符串及文件内容。使用迭代变量定义要检查的内容或字符串，并使用各种options选项进一步修改解析方式。使用options令牌选项指定哪些令牌应该作为迭代变量传递。请注意：在没有使用令牌选项时，`/F`将只检查第一个令牌。文件解析过程包括读取输出、字符串或文件内容，将其分成独立的文本行以及再将每行解析成零个或更多个令牌。然后通过设置为令牌的迭代变量值，调用for循环。默认情况下，`/F`传递每个文件每一行的第一个空白分隔符号。跳过空行。

详细的帮助格式为：

```
FOR /F ["options"] %variable IN (file-set) DO command [command-parameters]
FOR /F ["options"] %variable IN ("string") DO command [command-parameters]
FOR /F ["options"] %variable IN ('command') DO command [command-parameters]
```

带引号的字符串`"options"`包括一个或多个指定不同解析选项的关键字。这些关键字为：

**eol=c** 指一个行注释字符的结尾(就一个)

**skip=n** 指在文件开始时忽略的行数。

**delims=xxx** 指分隔符集。这个替换了空格和制表符的默认分隔符集。

**tokens=x,y,m-n** 指每行的哪一个符号被传递到每个迭代的for本身。这会导致额外变量名称的分配。m-n格式为一个范围。通过nth符号指定mth。如果符号字符串中的最后一个字符星号，那么额外的变量将在最后一个符号解析之后分配并接受行的保留文本。

**usebackq** 使用后引号（键盘上数字1左边的那个键\`）。未使用参数usebackq 时：file-set表示文件，但不能含有空格；双引号表示字符串，即"string"；单引号表示执行命令，即'command'。使用参数usebackq时：file-set和"file-set"都表示文件，当文件路径或名称中有空格时，就可以用双引号括起来；单引号表示字符串，即'string'；后引号表示命令执行，即\`command\`

For命令例1：

```
@echo off
rem 首先建立临时文件 test.txt
echo ;注释行,这是临时文件,用完删除 >test.txt
echo 11 段 12 段 13 段 14 段 15 段 16 段 >>test.txt
echo 21 段,22 段,23 段,24 段,25 段,26 段 >>test.txt
echo 31 段-32 段-33 段-34 段-35 段-36 段 >>test.txt
FOR /F "eol=; tokens=1,3* delims=,- " %%i in (test.txt) do echo %%i %%j %%k
Pause
Del test.txt
```

运行显示结果：

```
11 12 段 13 段 14 段 15 段 16 段
21 22 段,23 段,24 段,25 段,26 段
31 32 段-33 段-34 段-35 段-36 段
请按任意键继续...
```

解释：

`eol=;` 分号开头的行为注释行
`tokens=1,3*` 将每行第1段,第3段和剩余字段分别赋予变量%%i，%%j，%%k
`delims=,- ` （减号后有一空格）以逗号减号和空格为分隔符，空格必须放在最后

笔者注：如果不好理解可以更改相应值来观察编号。

For命令例2：

```
@echo off
FOR /F "eol= delims=" %%i in (test.txt) do echo %%i
Pause
```

运行将显示test.txt全部内容，包括注释行。

另外`/F`参数还可以输出命令的结果，For命令例3：

```
@echo off
FOR /F "delims=" %%i in ('net user') do @echo %%i
pause
```

这样你本机全部帐号名字就出来了。把扩号内的内容用两个单引号引起来就表示那个当命令执行，FOR会返回命令的每行结果，加"delims="是为了让含有空格的行能整行显示出来，不加就只显示空格左边一列！